# Copyright 2002-2005 Interchange Development Group (http://www.icdevgroup.org/)
# Licensed under the GNU GPL v2. See file LICENSE for details.
# $Id: selected.coretag,v 1.8 2006-03-27 22:21:13 docelic Exp $

UserTag selected            Order        name value
UserTag selected            addAttr
UserTag selected            PosNumber    2
UserTag selected            Version      $Revision: 1.8 $
UserTag selected            Routine      <<EOR
# Returns ' SELECTED' when a value is present on the form
# Must match exactly, but NOT case-sensitive
sub {
	my ($field,$value,$opt) = @_;
	$value = '' unless defined $value;
	my $ref = $opt->{cgi} ? $CGI::values{$field} : $::Values->{$field};
	return ' selected="selected"' if ! length($ref) and $opt->{default};

	if(! $opt->{case}) {
		$ref = lc($ref);
		$value = lc($value);
	}

	my $r = '';

	return ' selected="selected"' if $ref eq $value;

	if ($opt->{delimiter}) {
		$opt->{multiple} = 1;
	}

	if ($opt->{multiple}) {
	    
	    my $be;
	    my $ee;
	    $opt->{delimiter} = "\0" unless defined $opt->{delimiter};

	    if (length $opt->{delimiter}) {
		my $del = Vend::Interpolate::get_joiner($opt->{delimiter}, "\0");
		$be = '(?:^|' . $del . ')'; ;
		$ee = '(?:$|' . $del . ')'; ;
	    }
	    else {
		$be = '';
		$ee = '';
	    }

	    my $regex = qr/$be\Q$value\E$ee/;
	    return ' selected="selected"' if $ref =~ $regex;
	}

	return '';
}
EOR
